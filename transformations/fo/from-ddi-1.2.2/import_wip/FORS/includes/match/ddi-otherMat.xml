<?xml version='1.0' encoding='utf-8' ?>

<!-- ddi:otherMat -->
<xsl:template match="ddi:otherMat"
              xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
              xmlns:fo="http://www.w3.org/1999/XSL/Format">

    <!-- content -->
    <fo:block id="{generate-id()}" background-color="{$color-gray1}" space-after="2mm">

        <!-- title -->
        <fo:block background-color="{$forsblue}" font-weight="bold" text-indent="2mm"
                  color="white" padding-top="2mm" padding-bottom="1mm" >
            <fo:inline >
                <xsl:choose>
                    <xsl:when test="normalize-space(ddi:labl)">
                        <xsl:value-of select="normalize-space(ddi:labl)"/>
                    </xsl:when>
                    <xsl:otherwise>*** <xsl:value-of select="$msg/*/entry[@key='Untitled']"/> ***</xsl:otherwise>
                </xsl:choose>
            </fo:inline>
        </fo:block>

        <fo:block font-size="8pt" font-style="italic" margin="1mm" margin-top="1mm"
                  margin-bottom="1mm" linefeed-treatment="preserve" padding-top="0in" >
            <!-- subtitle -->
            <xsl:if test="normalize-space(ddi:notes[@type='dcterms:alternative'][1])">
                <fo:inline font-style="italic">
                    <xsl:value-of select="normalize-space(ddi:notes[@type='dcterms:alternative'][1])"/>
                </fo:inline>
                <xsl:text>, </xsl:text>
            </xsl:if>
            <!-- author -->
            <xsl:if test="normalize-space(ddi:notes[@type='dc:creator'][1])">
                <xsl:value-of select="normalize-space(ddi:notes[@type='dc:creator'][1])"/>
                <xsl:text>, </xsl:text>
            </xsl:if>
            <!-- date -->
            <xsl:if test="normalize-space(ddi:notes[@type='dcterms:created'][1])">
                <xsl:variable name="date" select="normalize-space(ddi:notes[@type='dcterms:created'][1])"/>
                <xsl:if test="string-length($date) >= 7">
                    <xsl:call-template name="isodate-month">
                        <xsl:with-param name="isodate" select="$date"/>
                    </xsl:call-template>
                    <xsl:text> </xsl:text>
                </xsl:if>
                <xsl:if test="string-length($date) >= 4">
                    <xsl:value-of select="substring($date,1,4)"/>
                </xsl:if>
                <xsl:text>, </xsl:text>
            </xsl:if>
            <!-- country -->
            <xsl:if test="normalize-space(ddi:notes[@type='dcterms:spatial'][1])">
                <xsl:value-of select="normalize-space(ddi:notes[@type='dcterms:spatial'][1])"/>
                <xsl:text>, </xsl:text>
            </xsl:if>
            <!-- language -->
            <xsl:value-of select="normalize-space(ddi:notes[@type='dc:language'][1])"/>
            <xsl:if test="normalize-space(ddi:notes[@type='dc:language'][1])">
                <xsl:text>, </xsl:text>
            </xsl:if>
            <!-- source -->
            <xsl:if test="normalize-space(@URI)">
                <xsl:value-of select="normalize-space(@URI)"/>
            </xsl:if>
        </fo:block>

        <!-- description -->
        <xsl:if test="boolean($show-documentation-description)">
            <xsl:if test="normalize-space(ddi:notes[@type='dc:description'][1])">
                <fo:block background-color="{$color-gray2}" font-size="8pt" font-weight="bold" text-indent="2mm" padding-top="1mm" padding-bottom="1mm" space-before="0mm">
                    <xsl:value-of select="$msg/*/entry[@key='Description']"/>
                </fo:block>
                <fo:block font-size="8pt" margin="2mm" linefeed-treatment="preserve" padding-top="1mm" padding-bottom="1mm" >
                    <xsl:call-template name="trim">
                        <xsl:with-param name="s" select="ddi:notes[@type='dc:description'][1]"/>
                    </xsl:call-template>					
                </fo:block>
            </xsl:if>
        </xsl:if>

        <!-- abstract -->
        <xsl:if test="boolean($show-documentation-abstract)">
            <xsl:if test="normalize-space(ddi:notes[@type='dcterms:abstract'][1])">
                <fo:block background-color="{$color-gray2}" font-size="8pt" font-weight="bold" text-indent="2mm" padding-top="1mm" padding-bottom="1mm" space-before="3mm">
                    <xsl:value-of select="$msg/*/entry[@key='Abstract']"/>
                </fo:block>
                <fo:block font-size="8pt" margin="2mm" linefeed-treatment="preserve" padding-top="1mm" padding-bottom="1mm" >
                    <xsl:call-template name="trim">
                        <xsl:with-param name="s" select="ddi:notes[@type='dcterms:abstract'][1]"/>
                    </xsl:call-template>					
                </fo:block>
            </xsl:if>
        </xsl:if>

        <!-- toc-->
        <xsl:if test="boolean($show-documentation-toc)">
            <xsl:if test="normalize-space(ddi:notes[@type='dcterms:tableOfContents'][1])">
                <fo:block background-color="{$color-gray2}" font-size="8pt" font-weight="bold" text-indent="2mm" padding-top="1mm" padding-bottom="1mm" space-before="0mm">
                    <xsl:value-of select="$msg/*/entry[@key='Table_of_Contents']"/>
                </fo:block>
                <fo:block font-size="8pt" margin="2mm" linefeed-treatment="preserve" padding-top="1mm" padding-bottom="1mm" >
                    <xsl:call-template name="trim">
                        <xsl:with-param name="s" select="ddi:notes[@type='dcterms:tableOfContents'][1]"/>
                    </xsl:call-template>					
                </fo:block>
            </xsl:if>
        </xsl:if>

        <!-- subjects -->
        <xsl:if test="boolean($show-documentation-subjects)">
            <xsl:if test="normalize-space(ddi:notes[@type='dc:subject'])">
                <fo:block background-color="{$color-gray2}" font-size="8pt" font-weight="bold" text-indent="2mm" padding-top="1mm" padding-bottom="1mm" space-before="0mm">
                    <xsl:value-of select="$msg/*/entry[@key='Subjects']"/>
                </fo:block>
                <fo:block font-size="8pt" margin="2mm" linefeed-treatment="preserve" padding-top="1mm" padding-bottom="1mm" >
                    <xsl:for-each select="ddi:notes[@type='dc:subject']">
                        <xsl:if test="position()>1">
                            <xsl:text>, </xsl:text>
                        </xsl:if>
                        <xsl:value-of select="normalize-space(.)"/>
                    </xsl:for-each>
                </fo:block>
            </xsl:if>
        </xsl:if>
        
    </fo:block>
</xsl:template>