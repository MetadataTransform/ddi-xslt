<?xml version='1.0' encoding='utf-8' ?>

<!-- ddi:var -->
<xsl:template match="ddi:var"
              xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
              xmlns:fo="http://www.w3.org/1999/XSL/Format">

    <!-- params -->
    <xsl:param name="fileId" select="./@files" />
 
    <!-- content -->
    <!-- VARIABLE TABLE -->
    <fo:table-row text-align="center" vertical-align="top"  keep-together.within-page="always" >
            <fo:table-cell>
                <fo:table id="var-{@ID}" table-layout="fixed" width="100%" font-size="8pt" space-after="0.3in">
                    <fo:table-column column-width="proportional-column-width(15)"/>
                    <fo:table-column column-width="proportional-column-width(85)"/>
                    <!-- HEADER AP_MODIF {$forsblue}-->
                    <fo:table-header>
                        <!-- SEPARATOR -->
                        <fo:table-row height="2mm" border-top="{$default-border}" border-bottom="{$default-border}">
                            <fo:table-cell number-columns-spanned="2">
                                <fo:block/>
                            </fo:table-cell>
                        </fo:table-row>
                        <fo:table-row background-color="{$forsblue}" text-align="center">						
                            <fo:table-cell number-columns-spanned="2" font-size="10pt" color ="white" font-weight="normal" text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                <fo:block>
                                    <fo:inline font-size="10pt" font-weight="normal">
                                        <xsl:value-of select="position()"/>
                                        <xsl:text> </xsl:text>
                                    </fo:inline>
                                    <fo:inline font-size="10pt" font-weight="bold">
                                        <xsl:value-of select="./@name"/>
                                    </fo:inline>
                                    <xsl:if test="normalize-space(./ddi:labl)">
                                        <xsl:text> </xsl:text>
                                        <xsl:value-of select="normalize-space(./ddi:labl)"/>
                                    </xsl:if>
                                </fo:block>
                            </fo:table-cell>
                        </fo:table-row>
                    </fo:table-header>
                    <!-- BODY -->
                    <fo:table-body>
                        <!-- Informations -->
                        <fo:table-row text-align="center" vertical-align="top">
                            <fo:table-cell font-weight="bold" text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                <fo:block>
                                    <xsl:value-of select="$msg/*/entry[@key='Information']"/>
                                </fo:block>
                            </fo:table-cell>
                            <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                <fo:block text-indent="1mm">
                                    <!-- type -->
                                    <xsl:if test="normalize-space(@intrvl)">
                                        <xsl:value-of select="$msg/*/entry[@key='Measure_Level']"/>:
                                        <xsl:choose>
                                            <xsl:when test="@intrvl='discrete'">
                                                <xsl:value-of select="$msg/*/entry[@key='discrete']"/>
                                            </xsl:when>
                                            <xsl:when test="@intrvl='ordinal'">
                                                <xsl:value-of select="$msg/*/entry[@key='ordinal']"/>
                                            </xsl:when>
                                            <xsl:when test="@intrvl='contin'">
                                                <xsl:value-of select="$msg/*/entry[@key='continuous']"/>
                                            </xsl:when>
                                        </xsl:choose>
                                        <xsl:text>, </xsl:text>
                                    </xsl:if>
                                    <!-- format -->
                                    <xsl:for-each select="ddi:varFormat">
                                        <xsl:value-of select="$msg/*/entry[@key='Format']"/>: 
                                        <!-- <entry key="Num">numeric</entry> -->
                                        <xsl:choose>
                                            <xsl:when test="@type='character'">
                                                <xsl:value-of select="$msg/*/entry[@key='Char']"/>
                                            </xsl:when>
                                            <xsl:when test="@type='numeric'">
                                                <xsl:value-of select="$msg/*/entry[@key='Num']"/>
                                            </xsl:when>
                                        </xsl:choose>
										
                                        <xsl:if test="normalize-space(ddi:location/@width)">
                                            <xsl:text>-</xsl:text>
                                            <xsl:value-of select="ddi:location/@width"/>
                                        </xsl:if>
                                        <xsl:if test="normalize-space(@dcml)">
                                            <xsl:text>.</xsl:text>
                                            <xsl:value-of select="@dcml"/>
                                        </xsl:if>
                                        <xsl:text>, </xsl:text>
                                    </xsl:for-each>
                                    <!-- range -->
                                    <xsl:for-each select="ddi:valrng/ddi:range">
                                        <xsl:value-of select="$msg/*/entry[@key='Range']"/>:
										
                                        <!-- (FK) Numeric format mask START -->
                                        <xsl:call-template name="NumFormat">
                                            <xsl:with-param name="input" select="@min"/>
                                        </xsl:call-template>
                                        <!-- (FK) Numeric format mask START -->
                                        <xsl:text>-</xsl:text>
                                        <!-- (FK) Numeric format mask START -->
                                        <xsl:call-template name="NumFormat">
                                            <xsl:with-param name="input" select="@max"/>
                                        </xsl:call-template>
                                        <!-- (FK) Numeric format mask START -->	
										
                                        <xsl:text>, </xsl:text>
                                    </xsl:for-each>
                                    <!-- missing -->
                                    <xsl:value-of select="$msg/*/entry[@key='Missing']"/>
                                    <xsl:text>: *</xsl:text>
                                    <xsl:for-each select="ddi:invalrng/ddi:item">
                                        <xsl:text>/</xsl:text>
                                        <xsl:value-of select="@VALUE"/>
                                    </xsl:for-each>
                                </fo:block>
                            </fo:table-cell>
                        </fo:table-row>
                        <!-- statistics -->
                        <xsl:variable name="statistics" select="ddi:sumStat[contains('vald invd mean stdev',@type)]"/>
                        <xsl:if test="$statistics">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell font-weight="bold" text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block>
                                        <xsl:value-of select="$msg/*/entry[@key='Statistics']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <!-- summary statistics -->
                                        <xsl:for-each select="$statistics[not(@wgtd)]">
                                            <xsl:variable name="type" select="@type"/>
                                            <xsl:if test="position()>1">, </xsl:if>
                                            <xsl:variable name="label">
                                                <xsl:choose>
                                                    <xsl:when test="@type='vald' ">
                                                        <xsl:value-of select="$msg/*/entry[@key='Valid']"/>
                                                    </xsl:when>
                                                    <xsl:when test="@type='invd' ">
                                                        <xsl:value-of select="$msg/*/entry[@key='Invalid']"/>
                                                    </xsl:when>
                                                    <xsl:when test="@type='mean' ">
                                                        <xsl:value-of select="$msg/*/entry[@key='Mean']"/>
                                                    </xsl:when>
                                                    <xsl:when test="@type='stdev' ">
                                                        <xsl:value-of select="$msg/*/entry[@key='StdDev']"/>
                                                    </xsl:when>
                                                    <xsl:otherwise>
                                                        <xsl:value-of select="@type"/>
                                                    </xsl:otherwise>
                                                </xsl:choose>
                                            </xsl:variable>
                                            <!-- <xsl:text> [</xsl:text> -->
                                            <xsl:value-of select="$label"/>
                                            <xsl:text>=</xsl:text>
											
                                            <xsl:choose>
                                                <xsl:when test="@type='mean' ">
                                                    <!-- (FK) Numeric format mask START -->
                                                    <xsl:call-template name="NumFormat2">
                                                        <xsl:with-param name="input" select="."/>
                                                    </xsl:call-template>
                                                    <!--FK) Numeric format mask START -->
                                                </xsl:when>
                                                <xsl:when test="@type='stdev' ">
                                                    <!-- (FK) Numeric format mask START -->
                                                    <xsl:call-template name="NumFormat2">
                                                        <xsl:with-param name="input" select="."/>
                                                    </xsl:call-template>
                                                    <!--FK) Numeric format mask START -->
                                                </xsl:when>
                                                <xsl:otherwise>
                                                    <xsl:value-of select="normalize-space(.)"/>
                                                </xsl:otherwise>
                                            </xsl:choose>
                                        </xsl:for-each>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- definition  -->
                        <xsl:if test="normalize-space(./ddi:txt)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Definition']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:txt"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- universe  -->
                        <xsl:if test="normalize-space(./ddi:universe)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Universe']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:universe"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- source -->
                        <xsl:if test="normalize-space(./ddi:respUnit)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Source']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:respUnit"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- pre-question -->
                        <xsl:if test="normalize-space(./ddi:qstn/ddi:preQTxt)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Pre-question']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:qstn/ddi:preQTxt"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- question -->
                        <xsl:if test="normalize-space(./ddi:qstn/ddi:qstnLit)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Literal_question']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:qstn/ddi:qstnLit"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- post-question -->
                        <xsl:if test="normalize-space(./ddi:qstn/ddi:postQTxt)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Post-question']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:qstn/ddi:postQTxt"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- interviewer instructions -->
                        <xsl:if test="normalize-space(./ddi:qstn/ddi:ivuInstr)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Interviewers_instructions']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:qstn/ddi:ivuInstr"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- imputation -->
                        <xsl:if test="normalize-space(./ddi:imputation)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Imputation']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:imputation"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- Recoding -->
                        <xsl:if test="normalize-space(./ddi:codInstr)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Recoding_and_Derivation']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:codInstr"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- Security -->
                        <xsl:if test="normalize-space(./ddi:security)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Security']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:security"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- Concepts -->
                        <xsl:if test="normalize-space(./ddi:concept)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Concepts']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block>
                                        <xsl:for-each select="./ddi:concept">
                                            <xsl:if test="position()>1">, </xsl:if>
                                            <xsl:value-of select="normalize-space(.)"/>
                                        </xsl:for-each>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>

                        <!-- Source -->
                        <xsl:if test="normalize-space(./ddi:verStmt/ddi:version)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Version_source']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:verStmt/ddi:version"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>

                        <!-- Notes -->
                        <xsl:if test="normalize-space(./ddi:verStmt/ddi:notes)">
                            <fo:table-row text-align="center" vertical-align="top">
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block font-weight="bold">
                                        <xsl:value-of select="$msg/*/entry[@key='Notes']"/>
                                    </fo:block>
                                </fo:table-cell>
                                <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                                    <fo:block text-indent="1mm">
                                        <xsl:apply-templates select="./ddi:verStmt/ddi:notes"/>
                                    </fo:block>
                                </fo:table-cell>
                            </fo:table-row>
                        </xsl:if>
                        <!-- Categories -->
                        <xsl:if test="$show-variables-description-categories=1 and normalize-space(./ddi:catgry)">
                            <xsl:variable name="category-count" select="count(ddi:catgry)"/>
                            <fo:table-row text-align="center" vertical-align="top">
                                <xsl:choose>
                                    <xsl:when test="number($show-variables-description-categories-max) >= $category-count">
                                        <!-- VAR CATEGORIES TABLE -->
                                        <fo:table-cell text-align="left" border="{$default-border}" number-columns-spanned="2">
                                            <!-- check if variable is weighted -->
                                            <xsl:variable name="is-weighted" select="count(ddi:catgry/ddi:catStat[@type='freq' and @wgtd='wgtd' ]) > 0"/>
                                            <!-- get the frequency statistics nodes -->
                                            <xsl:variable name="catgry-freq-nodes" select="ddi:catgry[not(@missing='Y')]/ddi:catStat[@type='freq']"/>
                                            <!-- compute frequency totals -->
                                            <xsl:variable name="catgry-sum-freq" select="sum($catgry-freq-nodes[ not(@wgtd='wgtd') ])"/>
                                            <xsl:variable name="catgry-sum-freq-wgtd" select="sum($catgry-freq-nodes[ @wgtd='wgtd'])"/>
                                            <!-- compute frequency maximum -->
                                            <xsl:variable name="catgry-max-freq">
                                                <xsl:call-template name="math:max">
                                                    <xsl:with-param name="nodes" select="$catgry-freq-nodes[ not(@wgtd='wgtd') ]"/>
                                                </xsl:call-template>
                                            </xsl:variable>
                                            <xsl:variable name="catgry-max-freq-wgtd">
                                                <xsl:call-template name="math:max">
                                                    <xsl:with-param name="nodes" select="$catgry-freq-nodes[@type='freq' and @wgtd='wgtd' ]"/>
                                                </xsl:call-template>
                                            </xsl:variable>
                                            <!-- render the table -->
                                            <fo:table id="var-{@ID}-cat" table-layout="fixed" width="100%" font-size="8pt">
                                                <fo:table-column column-width="proportional-column-width(12)"/>
                                                <xsl:choose>
                                                    <xsl:when test="$is-weighted">
                                                        <fo:table-column column-width="proportional-column-width(33)"/>
                                                        <fo:table-column column-width="proportional-column-width(8)"/>
                                                        <fo:table-column column-width="proportional-column-width(12)"/>
                                                    </xsl:when>
                                                    <xsl:otherwise>
                                                        <fo:table-column column-width="proportional-column-width(45)"/>
                                                        <fo:table-column column-width="proportional-column-width(8)"/>
                                                    </xsl:otherwise>
                                                </xsl:choose>
                                                <xsl:variable name="bar-column-width" select="2.5"/>
                                                <fo:table-column column-width="{$bar-column-width}in"/>
                                                <!-- VAR CATEGORIES HEADER -->
                                                <fo:table-header>
                                                    <fo:table-row background-color="{$color-gray1}" text-align="left" vertical-align="top">
                                                        <fo:table-cell border="0pt solid white" padding="{$cell-padding}">
                                                            <fo:block text-align="center">
                                                                <xsl:value-of select="$msg/*/entry[@key='Value']"/>
                                                            </fo:block>
                                                        </fo:table-cell>
                                                        <fo:table-cell border="0pt solid white" padding="{$cell-padding}">
                                                            <fo:block >
                                                                <xsl:value-of select="$msg/*/entry[@key='Label']"/>
                                                            </fo:block>
                                                        </fo:table-cell>
                                                        <fo:table-cell padding="{$cell-padding}" text-align="center" >
                                                            <fo:block >
                                                                <xsl:value-of select="$msg/*/entry[@key='Cases_Abbreviation']"/>
                                                            </fo:block>
                                                        </fo:table-cell>
                                                        <xsl:if test="$is-weighted">
                                                            <fo:table-cell border="0 solid white" padding="{$cell-padding}" text-align="center" >
                                                                <fo:block >
                                                                    <xsl:value-of select="$msg/*/entry[@key='Weighted']"/>
                                                                </fo:block>
                                                            </fo:table-cell>
                                                        </xsl:if>
                                                        <fo:table-cell border="0pt solid white" padding="{$cell-padding}" text-align="left" >
                                                            <fo:block >
                                                                <xsl:value-of select="$msg/*/entry[@key='Percentage']"/>
                                                                <xsl:if test="$is-weighted"> (<xsl:value-of select="$msg/*/entry[@key='Weighted']"/>)</xsl:if>
                                                            </fo:block>
                                                        </fo:table-cell>
                                                    </fo:table-row>
                                                </fo:table-header>
                                                <!-- VAR CATEGORIES BODY -->
                                                <fo:table-body>
                                                    <xsl:for-each select="ddi:catgry">
                                                        <fo:table-row background-color="{$color-gray2}" text-align="center" vertical-align="top">
                                                            <!-- value -->
                                                            <fo:table-cell text-align="center" border="0pt solid white" padding="2pt">
                                                                <fo:block>
                                                                    <xsl:call-template name="trim">
                                                                        <xsl:with-param name="s" select="ddi:catValu"/>
                                                                    </xsl:call-template>
                                                                </fo:block>
                                                            </fo:table-cell>
                                                            <!-- label -->
                                                            <fo:table-cell text-align="left" border="0pt solid white" padding="2pt">
                                                                <fo:block>
                                                                    <xsl:call-template name="trim">
                                                                        <xsl:with-param name="s" select="ddi:labl"/>
                                                                    </xsl:call-template>
                                                                </fo:block>
                                                            </fo:table-cell>
                                                            <!-- frequency -->
                                                            <xsl:variable name="catgry-freq" select="ddi:catStat[@type='freq' and not(@wgtd='wgtd') ]"/>
                                                            <fo:table-cell text-align="right" padding-right="2mm" border="0pt solid white" padding="2pt">
                                                                <fo:block>

                                                                    <!-- (FK) Numeric format mask START -->
                                                                    <xsl:call-template name="NumFormat">
                                                                        <xsl:with-param name="input" select="$catgry-freq"/>
                                                                    </xsl:call-template>
                                                                    <!-- (FK) Numeric format mask START -->

                                                                </fo:block>
                                                            </fo:table-cell>
                                                            <!-- weighted frequency -->
                                                            <xsl:variable name="catgry-freq-wgtd" select="ddi:catStat[@type='freq' and @wgtd='wgtd' ]"/>
                                                            <xsl:if test="$is-weighted">
                                                                <fo:table-cell text-align="center" border="0pt solid white" padding="2pt">
                                                                    <fo:block>
                                                                        <xsl:call-template name="trim">
                                                                            <xsl:with-param name="s" select="format-number($catgry-freq-wgtd,'0.0')"/>
                                                                        </xsl:call-template>
                                                                    </fo:block>
                                                                </fo:table-cell>
                                                            </xsl:if>
                                                            <!-- BAR-->
                                                            <fo:table-cell text-align="left" border="0pt solid white" padding="2pt">
                                                                <!-- compute percentage -->
                                                                <xsl:variable name="catgry-pct">
                                                                    <xsl:choose>
                                                                        <xsl:when test="$is-weighted">
                                                                            <xsl:value-of select="$catgry-freq-wgtd div $catgry-sum-freq-wgtd"/>
                                                                        </xsl:when>
                                                                        <xsl:otherwise>
                                                                            <xsl:value-of select="$catgry-freq div $catgry-sum-freq"/>
                                                                        </xsl:otherwise>
                                                                    </xsl:choose>
                                                                </xsl:variable>
                                                                <!-- compute bar width (percentage of highest value minus some space to display the percentage value) -->
                                                                <xsl:variable name="tmp-col-width-1">
                                                                    <xsl:choose>
                                                                        <xsl:when test="$is-weighted">
                                                                            <xsl:value-of select="($catgry-freq-wgtd div $catgry-max-freq-wgtd) * ($bar-column-width - 0.5)"/>
                                                                        </xsl:when>
                                                                        <xsl:otherwise>
                                                                            <xsl:value-of select="($catgry-freq div $catgry-max-freq) * ($bar-column-width - 0.5)"/>
                                                                        </xsl:otherwise>
                                                                    </xsl:choose>
                                                                </xsl:variable>
                                                                <xsl:variable name="col-width-1">
                                                                    <xsl:choose>
                                                                        <xsl:when test="0.01 > $tmp-col-width-1">0.01</xsl:when>
                                                                        <xsl:otherwise>
                                                                            <xsl:value-of select="$tmp-col-width-1"/>
                                                                        </xsl:otherwise>
                                                                    </xsl:choose>
                                                                </xsl:variable>
                                                                <!-- compute remaining space for second column -->
                                                                <xsl:variable name="col-width-2" select="$bar-column-width - $col-width-1"/>
                                                                <!-- display the bar but not for missing values or if there was a problem computing the width -->
                                                                <xsl:choose>
                                                                    <xsl:when test="not(@missing='Y') and $col-width-1 > 0">
                                                                        <fo:table table-layout="fixed" width="100%">
                                                                            <fo:table-column column-width="{$col-width-1}in"/>
                                                                            <fo:table-column column-width="{$col-width-2}in"/>
                                                                            <fo:table-body>
                                                                                <fo:table-row>
                                                                                    <fo:table-cell background-color="{$color-gray4}">
                                                                                        <fo:block> </fo:block>
                                                                                    </fo:table-cell>
                                                                                    <fo:table-cell margin-left="0.05in">
                                                                                        <fo:block>															
                                                                                            <xsl:value-of select="format-number($catgry-pct , '#0.0%')"/>
                                                                                        </fo:block>
                                                                                    </fo:table-cell>
                                                                                </fo:table-row>
                                                                            </fo:table-body>
                                                                        </fo:table> <!-- end bar table -->
                                                                    </xsl:when>
                                                                    <xsl:otherwise>
                                                                        <fo:block/> <!-- since fop 0.95, table-cell cannot be enmtpy -->
                                                                    </xsl:otherwise>
                                                                </xsl:choose>
                                                            </fo:table-cell>
                                                        </fo:table-row>
                                                    </xsl:for-each>
                                                    <!-- category total -->
                                                    <!-- TODO -->
                                                </fo:table-body>
                                            </fo:table> <!-- end category table -->
                                            <fo:block font-weight="normal" color="{$forsblue}" font-size="6pt" space-before ="1mm">
                                                <xsl:value-of select="$msg/*/entry[@key='SumStat_Warning']"/>
                                            </fo:block>
                                        </fo:table-cell>
                                    </xsl:when>
                                    <xsl:otherwise>
                                        <fo:table-cell background-color="{$color-gray1}" text-align="center" font-style="italic" border="{$default-border}" number-columns-spanned="2" padding="{$cell-padding}">
                                            <fo:block>
                                                <xsl:value-of select="$msg/*/entry[@key='Frequency_table_not_shown']"/>
                                                <xsl:text> </xsl:text>(<xsl:value-of select="$category-count"/>
                                                <xsl:text> </xsl:text>
                                                <xsl:value-of select="$msg/*/entry[@key='Modalities']"/>)</fo:block>
                                        </fo:table-cell>
                                    </xsl:otherwise>
                                </xsl:choose>

                            </fo:table-row>
                            <!--separator-->
                            <fo:table-row height="2mm" border-top="{$default-border}" border-bottom="{$default-border}">
                                <fo:table-cell number-columns-spanned="2">
                                    <fo:block/>
                                </fo:table-cell>
                            </fo:table-row>

                        </xsl:if>
                    </fo:table-body>
                </fo:table> <!-- end of variable table -->
            </fo:table-cell>
        </fo:table-row>
</xsl:template>