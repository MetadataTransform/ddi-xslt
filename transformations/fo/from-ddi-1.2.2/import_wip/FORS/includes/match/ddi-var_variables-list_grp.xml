<?xml version='1.0' encoding='utf-8' ?>

<!-- ddi:var (variables list_grp) -->
<xsl:template match="ddi:var" mode="variables-list_grp"
              xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
              xmlns:fo="http://www.w3.org/1999/XSL/Format">

    <!-- params -->
    <xsl:param name="fileId" select="./@files" />

    <!-- content -->
    <fo:table-row text-align="center" vertical-align="top">
        <xsl:choose>
            <xsl:when test="position() mod 2 = 0">
                <xsl:attribute name="background-color">
                    <xsl:value-of select="$color-white"/>
                </xsl:attribute>
            </xsl:when>
            <xsl:otherwise>
                <xsl:attribute name="background-color">
                    <xsl:value-of select="$color-gray1"/>
                </xsl:attribute>
            </xsl:otherwise>
        </xsl:choose>

        <!-- Variable Name-->
        <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}" padding-left="7pt">
            <fo:block>
                <xsl:choose>
                    <xsl:when test="$show-variables-list = 1">
                        <fo:basic-link internal-destination="var-{@ID}" font-weight="bold"  color="{$forsblue}" font-size="9pt" >
                            <xsl:value-of select="./@name"/>
                        </fo:basic-link>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:value-of select="./@name"/>
                    </xsl:otherwise>
                </xsl:choose>
            </fo:block>
        </fo:table-cell>

        <!-- Variable Label -->
        <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
            <fo:block>
                <xsl:choose>
                    <xsl:when test="normalize-space(./ddi:labl)">
                        <xsl:value-of select="normalize-space(./ddi:labl)"/>
                    </xsl:when>
                    <xsl:otherwise>
                        <xsl:text>-</xsl:text>
                    </xsl:otherwise>
                </xsl:choose>
            </fo:block>
        </fo:table-cell>

        <!-- Filename AP modification -->
        <xsl:if test="count(/ddi:codeBook/ddi:fileDscr)> 1">
            <fo:table-cell text-align="left" border="{$default-border}" padding="{$cell-padding}">
                <fo:block>	
                    <xsl:variable name="XXX" select="./@files"/>
                    <xsl:for-each select="key('listFich', $XXX)">
                        <fo:basic-link internal-destination="file-{@ID}" text-decoration="none"  font-weight="bold"  color="{$forsblue}" font-size="9pt" >
                            <xsl:apply-templates select="ddi:fileTxt/ddi:fileName"/>
                        </fo:basic-link>
                    </xsl:for-each>
                </fo:block>
            </fo:table-cell>
        </xsl:if >

        <!-- Variable page -->
        <fo:table-cell text-align="center" border="{$default-border}" padding="{$cell-padding}">
            <fo:block>
                <fo:basic-link internal-destination="var-{@ID}" font-weight="bold"  color="{$forsblue}" font-size="9pt" >
                    <fo:page-number-citation ref-id="var-{@ID}"/>
                </fo:basic-link>
            </fo:block>
        </fo:table-cell>	
    </fo:table-row>
</xsl:template>		